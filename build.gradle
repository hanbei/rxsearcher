plugins {
    id "io.spring.dependency-management" version "0.6.0.RELEASE"
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'com.github.ben-manes.versions' version '0.13.0'
    id 'org.jetbrains.kotlin.jvm' version '1.1.1'
}

apply plugin: 'application'

mainClassName = "de.hanbei.rxsearch.server.VertxServer"

dependencies {
    compile project(":search")
    compile project(":searcher")
    compile project(":processing")
    compile project(":config")

    compile "io.vertx:vertx-core"
    compile "io.vertx:vertx-web"
    compile "io.vertx:vertx-dropwizard-metrics"

    compile "com.fasterxml.jackson.datatype:jackson-datatype-guava"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin"

    compile "ch.qos.logback:logback-classic"
    compile "org.slf4j:jul-to-slf4j"

    testCompile "guru.nidi.raml:raml-tester"
    testCompile "io.vertx:vertx-unit"
    testCompile "io.vertx:vertx-codegen"
    testCompile "org.glassfish.jersey.core:jersey-client"
}

allprojects {
    apply plugin: 'java'
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'jacoco'

    version = '0.2.0'
    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    dependencies {
        compile "com.google.guava:guava"
        compile "org.slf4j:slf4j-api"

        compile "org.jetbrains.kotlin:kotlin-stdlib"
        compile "com.fasterxml.jackson.core:jackson-databind"

        testCompile "junit:junit"
        testCompile 'org.mockito:mockito-core'
        testCompile 'org.hamcrest:hamcrest-all'
        testCompile 'io.kotlintest:kotlintest'
    }

    dependencyManagement {
        dependencies {
            dependency "com.fasterxml.jackson.core:jackson-databind:2.8.8"
            dependency "com.fasterxml.jackson.datatype:jackson-datatype-guava:2.8.8"
            dependency "com.fasterxml.jackson.module:jackson-module-kotlin:2.8.8"
            dependency "org.jooq:joox:1.4.0"

            dependency "com.google.guava:guava:21.0"
            dependency "commons-codec:commons-codec:1.9"

            dependencySet(group: "org.slf4j", version: "1.7.25") {
                entry "slf4j-api"
                entry "jul-to-slf4j"
                entry 'slf4j-nop'
            }
            dependency "ch.qos.logback:logback-classic:1.2.2"

            dependency 'io.reactivex.rxjava2:rxjava:2.0.8'
            dependency 'org.asynchttpclient:async-http-client:2.0.31'
            dependency 'io.dropwizard.metrics:metrics-core:3.2.2'

            dependencySet(group: 'io.vertx', version: '3.4.1') {
                entry 'vertx-core'
                entry 'vertx-web'
                entry 'vertx-unit'
                entry 'vertx-dropwizard-metrics'
                entry 'vertx-codegen'
            }

            dependency 'org.codehaus.groovy:groovy-all:2.4.7'
            dependency "org.jetbrains.kotlin:kotlin-stdlib:1.1.1"

            // Test dependencies
            dependency("junit:junit:4.12") {
                exclude 'org.hamcrest:hamcrest-core'
            }
            dependency("org.mockito:mockito-core:2.7.21") {
                exclude 'org.hamcrest:hamcrest-core'
            }
            dependency "org.hamcrest:hamcrest-all:1.3"
            dependency "guru.nidi.raml:raml-tester:0.8.10"
            dependency "org.glassfish.jersey.core:jersey-client:2.23.2"
            dependency "org.spockframework:spock-core:1.0-groovy-2.4"
            dependency "cglib:cglib-nodep:3.2.4"

            // Kotlin test dependencies
            dependency "io.kotlintest:kotlintest:2.0.0"
        }
    }

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }
}

task stage(dependsOn: ['clean', 'installDist'])
